//file:noinspection GrUnresolvedAccess
plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

android {
    compileSdkVersion rootProject.compile_sdk_version
    buildToolsVersion rootProject.build_tools_version

    defaultConfig {
        minSdkVersion rootProject.min_sdk_version
        targetSdkVersion rootProject.target_sdk_version

        buildConfigField "String", "HOST_URL", "\"${rootProject.HOST_URL}\""
        buildConfigField "String", "DATABASE_NAME", "\"${rootProject.DATABASE_NAME}\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility = 11
        targetCompatibility = 11
    }

    kotlinOptions {
        jvmTarget = '11'
    }

    packagingOptions {
        resources.excludes.add("META-INF/*")
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    /* data module which is the inner (lower) layer of the project knows only about domain layer
    * (a layer with interfaces which communicates with outer (top) level of the project ) */
    implementation project(':domain')

    // kotlin
    api "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    // Koin for Android
    api("io.insert-koin:koin-android:$insert_koin_version")
    // Koin core features
    api("io.insert-koin:koin-core:$insert_koin_version")

    // network
    api "com.squareup.okhttp3:okhttp:$okhttp_version"
    api "com.squareup.retrofit2:retrofit:$retrofit_version"
    api "com.squareup.retrofit2:converter-gson:$retrofit_version"
    api "com.google.code.gson:gson:$gson_version"
    api "com.auth0:java-jwt:$jwt_version"

    // database
    api "androidx.room:room-runtime:$room_version"
    api "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // test
    testImplementation "junit:junit:$junit_version"
    androidTestImplementation "androidx.test.ext:junit:$ext_junit_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_version"

    // testImplementation for pure JVM unit tests
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"

    // androidTestImplementation for Android instrumentation tests
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"
}
